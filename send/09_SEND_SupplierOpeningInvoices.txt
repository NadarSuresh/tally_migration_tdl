;; Shivgoraksh

[Collection: SEND_SupplierOp]
		
	Export Header	: "Accept: application/json"
	Export Header	: "Accept-Charset: utf-8"
	Export Header	: "Authorization: " + "token " + @@AuthToken

	Data Source: HTTP JSON: @@ERPNextHost + '/api/method/express_tally.receive_tally.supplier_opening'
;	Data Source: HTTP JSON: @@ERPNextHost + '/api/method/express_tally.receive_tally.create_address'

	RemoteRequest: SEND_SupplierOp : UTF8
	JSON Object Path: "message:1"
	
;[#Menu: ExpressIntegration]
;	
;	Add: Item: Send Supplier Opening : Call: SEND_SupplierOp

	[Function: SEND_SupplierOp_Start]
		
		10 : Start Timer: SEND_SupplierOp_Timer : 1
		
	[System: Events]
		
		SEND_SupplierOp_Timer : Timer: True: Call: SEND_SupplierOp


[Function: SEND_SupplierOp]

	Variable : vName  : String
	Variable : vMessage : String
	
	06 : Do If: ($$NumItems:SEND_SupplierOp_Ledgers = 0) :  Stop Timer: SEND_SupplierOp_Timer
	07 : Do If: ($$NumItems:SEND_SupplierOp_Ledgers = 0) :  Return

;	08 : Stop Timer: SEND_SupplierOp_Timer
	09 : Start Progress: 1 : "Sending Supplier Opening Invoices" : ""


	10 : Walk Collection: SEND_SupplierOp
	20 : 	If: $Status
	22 :	Start Batch Post
	25 : 		Walk Collection: data
	26 : 		Log: $name
	28 : 		Set: vName : $name
	29 : 		Set: vMessage : $message
	30 : 			Modify Object: (Ledger, ##vName).Bill_Status[1].Bill_Status : @@ERPNextResetFlag
	32 :			Modify Object: (Ledger, ##vName).Bill_Message[1].Bill_Message : ##vMessage
	35 : 			Do If: ##vMessage != "Success" :  Stop Timer: SEND_SupplierOp_Timer
	40 :		End Walk
	45 : 	End Batch Post
	50 : 	End If
	60 : End Walk
	

;[#Menu: Gateway of Tally]
;	
;	Add: Item: Request - SupplierOp: Display: SEND_SupplierOp

[Report : SEND_SupplierOp]
	
	Form: SEND_SupplierOp
	Variable: SVFromDate, SVToDate
	Set: SVFromDate : @@EnableERPNextClosingDate
	Set: SVToDate	: @@EnableERPNextClosingDate
	
	[Form: SEND_SupplierOp]
		
		Part: SEND_SupplierOp
		Delete: XMLTag

	[Part: SEND_SupplierOp]
		
		Line: SEND_SupplierOp
		Repeat : SEND_SupplierOp : SEND_SupplierOp_Ledgers
		Scroll: Vertical

		[Line: SEND_SupplierOp]
			
			Field: SEND_SupplierOp
			JSONTag: "data"
			Explode: SEND_SupplierOp_bills : $$NumItems:SEND_SupplierOp_Tally > 0
			Empty: $$Line > @@RecordsLimit or $$NumItems:SEND_SupplierOp_Tally =0

			[Field: SEND_SupplierOp]
				
				Set as: $Name
				JSONTag: "account"
		
		[Part: SEND_SupplierOp_bills]

		Line: SEND_SupplierOp_bills 
		Repeat: SEND_SupplierOp_bills : SEND_SupplierOp_Tally
		Scroll: Vertical
		
		[Line: SEND_SupplierOp_bills]
			;SEND_SupplierOp_name
			Field: SEND_SupplierOp_name, SEND_SupplierOp_Supplier, SEND_SupplierOp_company,SEND_SupplierOp_posting_date,+
					SEND_SupplierOp_due_date, SEND_SupplierOp_debit_to, SEND_SupplierOp_against_income_account,+
					SEND_SupplierOp_amount, SEND_SupplierOp_is_return

			JSONTag: "bills"
			
			[Field: SEND_SupplierOp_name]
				
				Json Tag: "bill_name"
				Set as: $bill_name

			[Field: SEND_SupplierOp_Supplier]
				
				Json Tag: "supplier"
				Set as: $Supplier

			[Field: SEND_SupplierOp_company]

				Json Tag: "company"
				Set as: @@ERPNextBranch

			[Field: SEND_SupplierOp_posting_date]

				Json Tag: "posting_date"
				Set as: $$getSqlDate:$posting_date

			[Field: SEND_SupplierOp_due_date]

				Json Tag: "due_date"
				Set as: $$getSqlDate:$due_date

			[Field: SEND_SupplierOp_debit_to]

				Json Tag: "credit_to"
				Set as: $credit_to
				
			[Field: SEND_SupplierOp_against_income_account]

				Json Tag: "against_expense_account"
				Set as: $against_expense_account
				
			[Field: SEND_SupplierOp_amount]

				Json Tag: "amount"
				Set as: if $$IsDr:$amount then $$Number:$amount * -1 else $$Number:$amount 
				Type: Number
				
			[Field: SEND_SupplierOp_is_return]
				
				Type: Number
				Set as : if $$IsDr:$amount then 1 else 0
				JSONTag: "is_return"


[Collection: SEND_SupplierOp_Ledgers]
	
	Parm Var: SVFromDate : Date: @@EnableERPNextClosingDate
	Parm Var: SVToDate	 : Date: @@EnableERPNextClosingDate

	Type: Ledger 
	Child Of: $$GroupSundryCreditors
	Belongs To: Yes
	Filter: Bill_Status
	Filter: Bill_HasClosing

;	Filter : Debug_Filter
;	
;	[System : Formula]
;		
;		Debug_Filter : $Name = "CR SEA BROOK INTERNATIONAL TRADERS FZE"


[Collection: SEND_SupplierOp_TallySrc]
	
	Parm Var: SVFromDate : Date: @@EnableERPNextClosingDate
	Parm Var: SVToDate	 : Date: @@EnableERPNextClosingDate

	Parm Var: vLEdgerName : String : $Name

	Type: Bills
	Child Of: $Name
	Belongs To: Yes
	

[Collection: SEND_SupplierOp_Tally]

	Parm Var: SVFromDate : Date: @@EnableERPNextClosingDate
	Parm Var: SVToDate	 : Date: @@EnableERPNextClosingDate

	Source Collection: SEND_SupplierOp_TallySrc
	Compute: bill_name 		: $Name
	Compute: Supplier	 	: if $$IsEmpty:($LedgerCode:Ledger:$Parent) then $Parent else $LedgerCode:Ledger:$Parent
	Compute: posting_date 	: $BillDate
	Compute: due_date		: if $$IsEmpty:$BillCreditPeriod then $BillDate else $$String:$BillCreditPeriod:UniversalDate 
	Compute: credit_to		: "Creditors" + " - " + @@EnableERPNextCmpAbbr
	Compute: against_expense_account : "Temporary Opening" + " - " + @@EnableERPNextCmpAbbr
	Compute: amount			: $ClosingBalance
	
	Option: LEDSupplierOnAccount Opt : NOt $$IsEmpty:($OnAccountValue:Ledger:#SEND_SupplierOp)

	[!Collection: LEDSupplierOnAccount Opt]

		Object: LEDSupplierOnAccount
	
	[Object: LEDSupplierOnAccount]
		
		bill_name 		: $LedgerCode:Ledger:#SEND_SupplierOp + "On Account" ;$Name
		supplier	 	: $LedgerCode:Ledger:#SEND_SupplierOp
		posting_date 	: @@EnableERPNextClosingDate
		due_date		: @@EnableERPNextClosingDate
		credit_to		: "creditors" + " - " + @@EnableERPNextCmpAbbr
		against_expense_account : "Temporary Opening" + " - " + @@EnableERPNextCmpAbbr
		amount			: $OnAccountValue:Ledger:#SEND_SupplierOp			
